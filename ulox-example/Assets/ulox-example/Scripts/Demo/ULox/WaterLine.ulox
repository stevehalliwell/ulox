/*
Code we expect from Unity's side;
SetUIText(string)
CreateFromPrefab(string):GameObject (returns a GameObject reference that we'll pass back to unity)
GetKey(string):bool
SetGameObjectPosition(GameObject, x, y, z)
ReloadScene - reload the current unity scene


Unity will find and call: 
- SetupGame in Start
- Update in Update
*/

//functions and data
var dt = 0;
var points = [];
var gos = [];

//TODO remove the x, just be where you are
data Circle
{
	x = 0,
	y = 0, 
	py = 0,
	anchorY = 0,
	pullY = 0,
	go = CreateFromPrefab("Circle"),
}

fun SetupGame()
{
	print ("Setting Up Game");

	var numToSpawn = 50;
	var startX = -12.5;
	var stepX = 0.5;

	for(var i = 0; i < numToSpawn; i += 1)
	{
		var circ = Circle();
		circ.x = startX + i * stepX;
		points.Add(circ);
		gos.Add(circ.go);
	}
}

fun Update()
{
	if(GetKey("escape")){ReloadScene();}

	var drag = Math.Exp(-3 * dt);
	var dampen = drag;
	var invDampen = 1 - dampen;
	var pullFactor = 1;

	var circCount = points.Count();
	for(var i = 0; i < circCount; i += 1)
	{
		var item = points[i];
		var px = 0;
		var py = 0;
		var y = item.y;
		var neigh;
			neigh = points[(i-3 + circCount) % circCount];
			py += (neigh.y - y) * 0.25;
			neigh = points[(i-2 + circCount) % circCount];
			py += (neigh.y - y) * 0.5;
			neigh = points[(i-1 + circCount) % circCount];
			py += (neigh.y - y);
			neigh = points[(i+1) % circCount];
			py += (neigh.y - y);
			neigh = points[(i+2)% circCount];
			py += (neigh.y - y) * 0.5;
			neigh = points[(i+3)% circCount];
			py += (neigh.y - y) * 0.25;

		item.pullY = py * pullFactor * dt;
	}


	if(GetKey("space")){RandomPullAll();}
	if(GetKey("r")){RandomPull();}
	if(GetKey("h")){HoldMiddle();}

	loop(points)
	{
		var dy = item.y - item.py;
		item.py = item.y;
		item.y = item.y + dy + item.pullY;

		item.y = item.y * dampen + item.anchorY * invDampen;

		item.pullY = 0;
	}

	SetListOfGoToListOfPositions(gos, points, "x", "y");
}

fun RandomPullAll()
{
	loop(points)
	{
		item.pullY = RandRange(-1, 1);
	}
}

fun RandomPull()
{
	var index = points.Count()/2;
	var item = points[index];
	item.pullY = RandRange(-1, 1);
}

fun HoldMiddle()
{
	var index = points.Count()/2;
	var item = points[index];
	item.y = 3;
}