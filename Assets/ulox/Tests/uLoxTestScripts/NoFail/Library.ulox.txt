test DebugLibraryTests
{
    testcase GenerateStackDump
    {
        var local = 2;
        var result = 0;
        var contains1 = "2";
        var contains2 = "0";
        
        result = GenerateStackDump();
        
        Assert.DoesContain(contains1,result);
        Assert.DoesContain(contains2,result);
    }

    testcase GenerateGlobalsDump
    {
        var expected = "Assert";
        var result;
        
        result = GenerateGlobalsDump();
        
        Assert.DoesContain(expected,result);
    }
}

test StandardClassesLibraryTests
{
    testcase ListAsArray
    {
        var list = List();
        var len = 5;
        list.Resize(len);

        for (var i = 0; i < list.Count(); i+= 1)
        {
            list.Set(i, i);
        }

        for (var i = 0; i < list.Count(); i+= 1)
        {
            Assert.AreEqual(i, list.Get(i));
        }
    }
    
    testcase ListAdd
    {
        var list = List();
        var len = 5;

        for (var i = 0; i < len; i+= 1)
        {
            list.Add(i);
        }

        for (var i = 0; i < list.Count(); i+= 1)
        {
            Assert.AreEqual(i, list.Get(i));
        }
    }
    
    testcase DynamicUsage
    {
        var expected = 6;
        var result = 0;

        var obj = Dynamic();
        obj.a = 1;
        obj.b = 2;
        obj.c = 3;
        obj.d = -1;
        var d = obj.a + obj.b + obj.c;
        obj.d = d;
        result = obj.d;

        Assert.AreEqual(expected, result);
    }
}

test DynamicLibraryTests
{
    testcase DynamicHasField
    {
        var expectedA = true;
        var expectedB = false;
        var resultA = false;
        var resultB = true;
        var obj = Dynamic();
        obj.a = 7;

        resultA = Dynamic.HasField(obj, "a");
        resultB = Dynamic.HasField(obj, "b");

        Assert.AreEqual(expectedA, resultA);
        Assert.AreEqual(expectedB, resultB);
    }

    testcase DynamicRemoveField
    {
        var expected = false;
        var result = 0;
        var obj = Dynamic();
        obj.a = 7;

        Dynamic.RemoveField(obj, "a");
        result = Dynamic.HasField(obj, "a");

        Assert.AreEqual(expected, result);
    }
}

test VMLibraryTests
{
    testcase InnerVmInsertAndExtract
    {
        var expected = 20;
        var startingValue = 10;
        var result = 0;
        var innerVM = VM();
        fun InnerMain()
        {
            aGlobal *= 2;
        }
        
        innerVM.AddGlobal("aGlobal", startingValue);
        innerVM.Start(InnerMain);

        result = innerVM.GetGlobal("aGlobal");

        Assert.AreEqual(expected, result);
    }

    testcase InnerVmAsGenerator
    {
        var expected = 20;
        var result = 0;
        var innerVM = VM();
        fun InnerMain()
        {
            globalOut = 2;
            yield;
            globalOut = 3;
            yield;
            globalOut = 5;
            yield;
            globalOut = 10;
            yield;
            globalOut = null;
        }

        innerVM.AddGlobal("globalOut",0);

        innerVM.Start(InnerMain);
        loop
        {
            var curVal = innerVM.GetGlobal("globalOut");
            if(curVal != null)
            {
                result += curVal;
                innerVM.Resume();
            }
            else
            {
                break;
            }
        }

        Assert.AreEqual(expected, result);
    }
}
