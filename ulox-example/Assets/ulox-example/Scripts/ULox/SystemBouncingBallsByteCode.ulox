/*
Code we expect from Unity's side;
SetUIText(string)
CreateFromPrefab(string):GameObject (returns a GameObject reference that we'll pass back to unity)
GetKey(string):bool
SetGameObjectPosition(GameObject, x, y, z)
ReloadScene - reload the current unity scene


Unity will find and call: 
- SetupGame in Start
- Update in Update
*/

//functions and data
var dt = 0;
var limit = 5;
var numBallsToSpawn = 100;

var ballName = "BouncyBall";
var balls = [];

fun RandVec2()
{
	return (RandRange(-3,3),RandRange(-3,3));
}

class Ball
{
	init()
	{
		var (x,y) = RandVec2();
		this.posx = x;
		this.posy = y;

		var (vx,vy) = RandVec2();
		this.velx = vx;
		this.vely = vy;
		
		this.go = CreateFromPrefab(ballName);
	}

	Tick()
	{
  		this.posx = this.posx + this.velx * dt;
  		this.posy = this.posy + this.vely * dt;

		this.Contain(limit);
		this.UpdateGoPosition();
	}
	
	Contain(lim)
	{
		var x = this.posx;
		var y = this.posy;
		var vx = this.velx;
		var vy = this.vely;

		if((x < -lim and vx < 0) or 
		  (x > lim  and vx > 0) )
		{
			vx *= -1;
			this.velx = vx;
		}

		if((y < -lim and vy < 0) or
		  (y > lim  and vy > 0) )
		{
			vy *= -1;
			this.vely = vy;
		}
	}
	
	UpdateGoPosition()
	{
		SetGameObjectPosition(this.go, this.posx, this.posy, 0);
	}
}

fun SetupGame()
{
	print ("Setting Up Game");

	for(var i = 0; i < numBallsToSpawn; i += 1)
	{
		balls.Add(Ball());
	}
}

fun Update()
{
	if(GetKey("escape")){ReloadScene();}

	loop(balls)
	{
		item.Tick();
	}
}
